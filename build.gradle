allprojects {
    group 'fr.minuskube.inv'
    version '1.3.5'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
    }

    javadoc {
        options.links(
                'https://hub.spigotmc.org/javadocs/spigot/',
                'https://docs.oracle.com/javase/1.8/docs/api/'
        )
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts { archives javadocJar, sourcesJar }

    signing {
        sign configurations.archives
    }
}

task createBukkitJar(type: Jar) {
    archiveFileName = 'SmartInvs-Bukkit-' + project.version + '.jar'
    from { project(':core').sourceSets.main.output }
    from { project(':bukkit').sourceSets.main.output }
}

task createFoliaJar(type: Jar) {
    archiveFileName = 'SmartInvs-Folia-' + project.version + '.jar'
    from { project(':core').sourceSets.main.output }
    from { project(':folia').sourceSets.main.output }
}